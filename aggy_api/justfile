set shell := ["sh", "-c"]
# NOTE: disabled to avoid issues with SQLx overrides
# set dotenv-load

# List the avail commands
default:
  @just --list --unsorted

# psql from the db running in the compose launched pg container
psql *ARGS:
  podman-compose -f ./docker-compose.yml -f ./docker-compose.dev.yml \
    exec postgres psql -U anf -d aggy {{ARGS}}

# psql command but fit for redirects
psql-tty *ARGS:
  podman-compose  -f ./docker-compose.yml -f ./docker-compose.dev.yml \
    exec -T postgres \
    psql -U anf -d aggy {{ARGS}}

# The flyway cli tool
flyway *ARGS:
  podman-compose \
    -f ./docker-compose.yml -f ../docker-compose.tools.yml \
    --profile tools run --rm \
    flyway {{ARGS}}

# Apply migrations to database.
db-mig:
  cargo sqlx database create
  @just flyway migrate

# Seed db from the fixtures.
db-seed:
  @just psql-tty < fixtures/000_test_data.sql  

# Apply migrations to database.
db-reset:
  cargo sqlx database drop -y
  @just db-mig 
  @just db-seed

alias dev := dev-up

# Start all services required for development
dev-up:
  podman-compose -f docker-compose.yml -f docker-compose.dev.yml up -d

dev-down *ARGS:
  podman-compose -f docker-compose.yml -f docker-compose.dev.yml down {{ARGS}}

logs-dev:
  podman-compose -f docker-compose.yml -f docker-compose.dev.yml logs -f -n -t

test *ARGS:
  cargo nextest run {{ARGS}}
